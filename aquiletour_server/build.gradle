plugins{
    id 'local-java-plugin'
    id 'local-jsweet-plugin'
    id 'com.github.hesch.execfork' version '0.1.15'
}

dependencies {
    // Ntro
    implementation project(':ntro_core')
    implementation project(':ntro_jdk')

    // aquiletour
    implementation project(':aquiletour_core')

    // Jetty web server
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.28.v20200408'

    // jsoup HTML parser library @ https://jsoup.org/
    implementation 'org.jsoup:jsoup:1.13.1'

    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '3.141.59'

    testImplementation 'junit:junit:4.13'
}

tasks.test{
    dependsOn 'testServer'
    afterTest { desc, result -> 
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

task server (type:JavaExec) {
    dependsOn 'classes'
    main = 'ca.aquiletour.server.JavaMainServer'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs('-Dfile.encoding=UTF-8','-Xverify:none') // -Xverify:none for profiling in jvisualvm
    standardInput = System.in
}

task testServer(type: com.github.psxpaul.task.JavaExecFork) {
    dependsOn 'classes'
    main = 'ca.aquiletour.server.JavaMainServer'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs('-Dfile.encoding=UTF-8') 
    classpath = sourceSets.main.runtimeClasspath
    stopAfter = tasks.test
    killDescendants=false
}
